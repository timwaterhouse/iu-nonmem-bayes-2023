$PROB example analysis

$INPUT C NUM ID TIME CMT EVID MDV DV AMT RATE DOSE LDOS SEX AGE WT EGFR BILI BLQ DAY TAD ADDL II NMTIME STUDY


$DATA ../../../data/study-001-002-003.csv
  IGNORE=@ 
  IGNORE=(BLQ.EQ.1)
  IGNORE=(TIME.LT.0)

$SUBROUTINE ADVAN4 TRANS4

$PK

  ; cap EGFR at 120
  GFRC = EGFR
  IF(EGFR.GT.120) GFRC = 120

  ; Patient vs healthy
  PAT=0
  IF(STUDY.EQ.3) PAT=1
  ; male vs female
  ISEX=0
  IF(SEX.EQ.2) ISEX=1

  WTCL = LOG(WT/70)*THETA(7)
  WTV = LOG(WT/70)*THETA(8)
  AGECL = LOG(AGE/40)*THETA(9)
  EGFRCL = LOG(GFRC/120)*THETA(10)
  PATCL = THETA(11)*PAT
  SEXCL = THETA(12)*ISEX

  MU_1 = LOG(THETA(1)) + WTCL + AGECL + EGFRCL + PATCL + SEXCL
  CL = EXP(MU_1 + ETA(1))

  MU_2 = LOG(THETA(2)) + WTV
  V2 = EXP(MU_2 + ETA(2))

  MU_3 = LOG(THETA(3))
  KA = EXP(MU_3 + ETA(3))

  MU_4 = LOG(THETA(4))
  D1 = EXP(MU_4 + ETA(4))

  MU_5 = LOG(THETA(5)) + WTCL
  Q = EXP(MU_5 + ETA(5))

  MU_6 = LOG(THETA(6)) + WTV
  V3 = EXP(MU_6 + ETA(6))

  S2 = V2

$ERROR
  IPRED = F
  Y = IPRED*(1 + EPS(1)) + EPS(2)

$THETA 
  (0 8)    ; CL
  (0 200)  ; V2
  (0 2)    ; KA
  (0,1)    ; D1
  (0,0.5)  ; Q
  (0,80)   ; V3

  (0.75)   ; WTCL
  (1.00)   ; WTV
  (0.01)   ; AGECL
  (0.01)   ; EGFRCL
  (0.01)   ; PATCL
  (0.01)   ; SEXCL

$OMEGA BLOCK(2)
  0.1  ; CL
  0.05 0.1  ; V2
$OMEGA
  0.1   ; KA
  0 FIX ; D1
  0 FIX ; Q
  0 FIX ; V3

$SIGMA
  0.1   ; PROP
  10    ; ADD

$EST NOABORT MAXEVAL=9999 PRINT=1 METH=1 INTER ETASTYPE=1
$COV PRINT=E

$TABLE NUM ID TIME DAY TAD CMT EVID DOSE IPRED CWRES NPDE STUDY
       FILE=101.tab ONEHEADER NOPRINT

$TABLE ID DOSE SEX WT AGE EGFR BILI KA D1 CL V2 Q V3 ETAS(1:LAST) STUDY
       FILE=101par.tab ONEHEADER NOPRINT FIRSTONLY
